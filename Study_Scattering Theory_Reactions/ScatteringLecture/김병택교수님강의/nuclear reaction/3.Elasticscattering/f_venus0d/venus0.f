      
      PROGRAM VENUS0
      PARAMETER(KK=10,KA=180)
	IMPLICIT REAL*8(A-H,O-Z)
     
      DIMENSION   KTRL(KK),KOUT(KK)
      DIMENSION   THETAD(KA)
				
      IR=5
      IW=6
      OPEN(IR,FILE='venus0.DAT',STATUS='OLD')
      OPEN(IW,FILE='venus0.OUT',STATUS='OLD')
						   
						   
C     ***************     READ INITIAL DATA     ***************
							       
   10 FORMAT(24I3)
   11 FORMAT(14I5)
   15 FORMAT(10F7.4)
		    
      READ(IR,10) (KTRL(N),N=1,10)
      READ(IR,10) (KOUT(N),N=1,10)
      READ(IR,15) TMST,TZ,PMST,PZ
      READ(IR,15) ELABI,XMES,THMIN,THMAX,THINC
      READ(IR,11) LMAX,NXMX
      READ(IR,15) VO,WO,WS1,AO,AI,AIS1,RO,RI,RIS1,RC
      READ(IR,15) WS2,AIS2,RIS2
     
      THETA=THMIN-THINC
      NOTH=(THMAX-THMIN)/THINC+1.0001
      DO 500 K=1,NOTH
      THETA=THETA+THINC
      THETAD(K)=THETA
  500 CONTINUE
      
C======================================================================
      CALL OPTMDL0(KTRL,KOUT,LMAX,NXMX,NOTH,
     1             TMST,PMST,TZ,PZ,ELABI,XMES,THETAD,
     2             VO,WO,WS1,WS2,AO,AI,AIS1,AIS2,RO,RI,RIS1,RIS2,RC)
C======================================================================

      STOP
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE OPTMDL0(KTRL,KOUT,LMAX,NXMAX,NAMAX,TMST,PMST,TZ,PZ,
     1                   ELABI,XMES,THETAD,VO,WO,WS1,WS2,AO,AI,
     2                   AIS1,AIS2,RO,RI,RIS1,RIS2,RC)
C======================================================================
C
C     PROGRAM NAME : OPTMDL0.FOR 
C
C     PURPOSE
C     DISTORTED WAVE, COULOMB PHASE SHIFT AND S-MATRICES CALCULATIONS 
C     FOR GIVEN OPTICAL MODEL PARAMETERS COSIDERING OPTICAL MODEL
C     WITHOUT SPIN-ORBIT INTERACTION.
C
C     REQUIRED SUBROUTINE
C     COUPHS(LMAX,ETA,  SIG)
C     POTEN(ICONT,NXMIN,NXMAX,XMES,VO,R0,AO,  VPOT)
C     DISWV0(LP,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,VRID,VFUS,
C    O       DWAVE,SM,OVEW)
C     ELCROS0(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
C    O        CRRUTH,CREL,CRATIO)
C     ELETC0(LMAX,WN,ETA,SMAT,FUST,  FCROS,TCROS)
C
C     INPUT DATA 
C     KTRL(KA=10)   : REACTION CONTROL CARD.
C     KOUT(KA=10)   : OUTPUT CONTROL CARD.
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTOM.(l+1)
C     NXMAX  : MAXIMUM NUMBER OF INTEGRATION RANGE.
C     NAMAX  : MAXIMUM NUMBER OF ANGLE.
C     TMST   : TARGET MASS.
C     PMST   : PROJECTIL MASS.
C     TZ     : TARGET CHARGE.
C     PZ     : PROJECTIL CHARGE.
C     ELABI  : LABORATORY ENERGY.
C     XMES   : MESH SIZE.
C     THETAD(KA=180): THE ANGLE IN THE UNIT OF DEGREE.(na)
C     VO, AO,  RO   : OPTICAL MODEL PARAMETER REAL PART.
C     WO, AI,  RI   : OPTICAL MODEL PARAMETER IMAGINARY PART.
C     WS1,AIS1,RIS1 : SURFACE INTERACTION PARAMETER IMAGINARY PART 1.
C     WS2,AIS2,RIS2 : SURFACE INTERACTION PARAMETER IMAGINARY PART 2.
C     RC     : COULOMB RADIUS PARAMETER.
C
C     RESULT
C     DWAVE(KX=500) : DISTORTED WAVE.(nx+1)
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1)
C     SMAT(KL=200)  : S-MATRIX.(l+1)
C
C----------------------------------------------------------------------
      
      PARAMETER(IW=6,KK=10,KL=200,KX=500,KA=180)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   KTRL(KK),KOUT(KK)
      DIMENSION   SIG(KL),FUST(KL),SMAT(KL)
      DIMENSION   VREAL(KX),VREPC(KX),WVOL(KX),WSUR1(KX),WSUR2(KX)
      DIMENSION   VFUS(KX),DWAVE(KX),VRID(KX)
      DIMENSION   THETAD(KA),CRRUTH(KA),CREL(KA),CRATIO(KA)
      COMPLEX*16     ZERO,TTI,EXSGRI,SM,SMAT,DWAVE,VRID

      KTRL1=KTRL(1)      !
      KTRL2=KTRL(2)      !=0, VOLUME IMAGINARY POT. ONLY FOR FUSION.
C                        !=1, VOLUME+SURFACE1 TERM ONLY.
C                        !=2, VOLUME+SURFACE1+SURFACE2.
      KTRL3=KTRL(3)      !=1, ELASTIC CROSS SECTIONS ARE CALCULATED.
      KTRL4=KTRL(4)      !=1, FUSION AND PARTIAL WAVE INFOR. ARE CALCULATED.
 
      KOUT1=KOUT(1)      !KINEMATICAL VALUES.
      KOUT2=KOUT(2)      !POTENTIAL VALUES.

      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D0)

      HBAR=197.327053D0
      AMU =931.49432D0
      FINE=137.0359895D0
      PI  =4.0D0*DATAN(1.0D00)
      WNUNIT=DSQRT(2.0D0*AMU)/HBAR
      ESQ   =HBAR/FINE
      HBRSQ =HBAR*HBAR

      TMPM=TMST**0.3333333333+PMST**0.3333333333
      IF (PMST.LT.4.0D0) TMPM=TMST**0.3333333333
      RMST=TMST*PMST/(TMST+PMST)
      TZPZ=TZ*PZ
      ELAB=ELABI
      ECM =ELAB*TMST/(TMST+PMST)
      WN  =WNUNIT*DSQRT(ECM*RMST)
      ETA =RMST*AMU*TZPZ*ESQ/(WN*HBRSQ)

      FAC1=40.0D0*PI*RMST*WNUNIT**2/WN**3
      FAC2=40.0D0*PI/WN**2

      IF (KOUT1.NE.0) THEN
	 WRITE(IW,700) ELABI
	 WRITE(IW,710) TMST,TZ,PMST,PZ
	 WRITE(IW,720)
	 WRITE(IW,722) (KTRL(N),N=1,10)
	 WRITE(IW,724) (KOUT(N),N=1,10)
	 WRITE(IW,730)
	 WRITE(IW,732) VO,AO,RO
	 WRITE(IW,734) WO,AI,RI
	 WRITE(IW,736) WS1,AIS1,RIS1
	 WRITE(IW,738) WS2,AIS2,RIS2
	 WRITE(IW,740) RC
	 WRITE(IW,750) LMAX
	 WRITE(IW,752) NXMAX,XMES
	 WRITE(IW,754) NAMAX,THETAD(1),THETAD(NAMAX)
	 WRITE(IW,760) ELABI
	 WRITE(IW,762) ECM
	 WRITE(IW,764) WN
	 WRITE(IW,766) ETA
      ENDIF
  700 FORMAT(1H1,/,1H ,'ELASTIC SCATTERING CROSS SECTIONS AT ELAB='
     1       F9.3)
  710 FORMAT(1H ,5X,'TARGET     MASS=',F8.2,' CHARGE=',F8.2/
     1       1H ,5X,'PROJECTILE MASS=',F8.2,' CHARGE=',F8.2//)
  720 FORMAT(1H ,'     N =  1  2  3  4  5  6  7  8  9 10 ')
  722 FORMAT(1H ,'KTRL(N)=',10I3)
  724 FORMAT(1H ,'KOUT(N)=',10I3)
  730 FORMAT(//,1H ,'OPTICAL MODEL PARAMETERS',/,
     1       14X,' DEPTH      DIFF    RADIUS')
  732 FORMAT(1H ,'REAL     =',3F10.5)
  734 FORMAT(1H ,'IMAGINARY=',3F10.5)
  736 FORMAT(1H ,'SUR.IMAG1=',3F10.5)
  738 FORMAT(1H ,'SUR.IMAG2=',3F10.5)
  740 FORMAT(1H ,'COULOMB  =',20X,F10.5)
  750 FORMAT(//,1H ,'LMAX             =',I6)
  752 FORMAT(   1H ,'NXMAX,XMES       =',I6,2F9.3)
  754 FORMAT(   1H ,'NAMAX,THMIN,THMAX=',I6,2F9.3)
  760 FORMAT(//,1H ,'ELAB  =',F9.3)
  762 FORMAT(   1H ,'ECM   =',F9.3)
  764 FORMAT(   1H ,'WN    =',F9.3)
  766 FORMAT(   1H ,'ETA   =',F9.3)

C**********************************************************************
CHECKING INPUT DATA
C      RMAX=DAMAX1(RO,RI,RIS1,RIS2,RC)
C      RMAX=RMAX*TMPM      
C      AMAX=DAMAX1(AO,AI,AIS1,AIS2)      
C      AMIN=DAMIN1(AO,AI,AIS1,AIS2)      
C      RMAX=2.2*AMAX+RMAX+1.0
C      NXCHK =RMAX/XMES
C      LCHK  =RMAX*WN
C      XTAMES=AMIN/5.0   
C      XTKMES=2.*PI/(WN*10.0)
C      XCHK  =DAMIN1(XTAMES,XTKMES)
C      NACHK =0
C      IF (THETAD(1).EQ.0.0.OR.THETAD(NAMAX).EQ.180.0) NACHK=1
C
C      IF (LCHK.GE.LMAX)   WRITE(IW,70) LMAX,LCHK
C      IF (NXCHK.GE.NXMAX) WRITE(IW,71) NXMAX,NXCHK
C      IF (XCHK.LT.XMES)   WRITE(IW,72) XMES,XCHK
C      IF (ETA.NE.0.0.AND.NACHK.EQ.1) WRITE(IW,73) 
C   70 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR LMAX =',I5,'   SUGGESTED VALUE=',I5)
C   71 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR NXMAX=',I5,'   SUGGESTED VALUE=',I5)
C   72 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR XMES =',F10.5,'   SUGGESTED VALUE=',F10.5)
C   73 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR ANGLE IS EITHER 0.0 OR 180.0.')
C********************************************************************

      CALL COUPHS(LMAX,ETA,SIG)

      ICONT=1
      NXMIN=1
      R0=RO*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,VO,R0,AO,VREAL)
      R0=RI*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WO,R0,AI,WVOL)
      ICONT=3
      R0=RIS1*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WS1,R0,AIS1,WSUR1)
      R0=RIS2*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WS2,R0,AIS2,WSUR2)
      ICONT=4
      V0=TZPZ
      R0=RC*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,V0,R0,AO,VREPC)

      DO 100 NX=NXMIN,NXMAX
	 VRID(NX)=VREAL(NX)+VREPC(NX)
     1           +TTI*(WVOL(NX)+WSUR1(NX)+WSUR2(NX))
	 VFUS(NX)=WVOL(NX)
  100 CONTINUE
      
      IF (KTRL2.EQ.1) THEN
	 DO 110 NX=NXMIN,NXMAX
	    VFUS(NX)=WVOL(NX)+WSUR1(NX)
  110    CONTINUE
      ENDIF

      IF (KTRL2.EQ.2) THEN
	 DO 120 NX=NXMIN,NXMAX
	    VFUS(NX)=WVOL(NX)+WSUR1(NX)+WSUR2(NX)
  120    CONTINUE
      ENDIF

      IF (KOUT2.NE.0) THEN
	 XMIN=DFLOAT(NXMIN-1)*XMES
	 XMAX=DFLOAT(NXMAX-1)*XMES           
	 XINC=DFLOAT(KOUT2)*XMES
	 WRITE(IW,800) XMIN,XMAX,XINC
	 WRITE(IW,810)
	 WRITE(IW,812) (VREAL(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,820)
	 WRITE(IW,812) (VREPC(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,830)
	 WRITE(IW,812) (WVOL(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,840)
	 WRITE(IW,812) (WSUR1(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,850)
	 WRITE(IW,812) (WSUR2(NX),NX=NXMIN,NXMAX,KOUT2)
      ENDIF
  800 FORMAT(//,1H ,'OPTICAL POTENTIAL FROM',F8.3,' TO',F8.3,
     1              ' IN STEPS OF',F10.4)
  810 FORMAT(/,1H ,' REAL PART')
  812 FORMAT(  1H ,5E12.5)
  820 FORMAT(/,1H ,' COULOMB PART')
  830 FORMAT(/,1H ,' IMAGINARY VOLUME PART')
  840 FORMAT(/,1H ,' IMAGINARY SURFACE1 PART')
  850 FORMAT(/,1H ,' IMAGINARY SURFACE2 PART')

      DO 200 L=1,LMAX
	 FUST(L)=0.0
	 SMAT(L)=ZERO
  200 CONTINUE

      DO 300 LP1=1,LMAX
	 LP=LP1-1
	 FL2=DFLOAT(2*LP+1)
	 EXSGRI=CDEXP(TTI*SIG(LP1))

	 CALL DISWV0(LP,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,VRID,VFUS,
     1                DWAVE,SM,OVEW)
	 SMAT(LP1)=SM
	 FUST(LP1)=OVEW*FL2*FAC1

  300 CONTINUE

      IF (KTRL3.NE.0) THEN
	 CALL ELCROS0(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
     1               CRRUTH,CREL,CRATIO)
      ENDIF

      IF (KTRL4.NE.0) THEN
	 CALL ELETC0(LMAX,WN,ETA,SMAT,FUST,
     1              FCROS,TCROS)
      ENDIF

      RETURN
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE COUPHS(LMAX,ETA,
     O                  SIG)
C=======================================================================
C
C     PROGRAM NAME : COUPHS.FOR
C
C     PURPOSE
C     CALCULATION OF COULOMB PHASE SHIFT UP TO MAXIMUM ANGULAR MOMENTUM 
C     FOR A GIVEN ETA.
C
C     REFERENCE
C     1. JOHN M.BLATT, VICTOR F.WEISSKOPF, THEORETICAL NUCLEAR PHYSICS,
C     (JOHN WILEY, NEW YORK, 1952) 330. EQ.2.35.
C     2. MILTON ABRAMOWITZ AND IRENE A.STEGUN, HANDBOOK OF MATHEMATICAL 
C     FUNCTIONS WITH FORMULAS, GRAPHS, AND MATHEMATICAL TABLES,
C     (NATIONAL BUREAU OF STANDARDS, WASHINGTON, 1964) 256-257. 
C     EQ.6.1.24, EQ.6.1.40, EQ.6.1.41.
C     3. JOHN R.TAYLOR, SCATTERING THEORY, (JOHN WILEY, NEW YORK, 1972) 
C     261. EQ.14.4. 
C
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C
C     RESULTS
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1)
C
C     EXAMPLE
C     LMAX    ETA     SIG(1/6        2/7        3/8        4/9       5/10)    
C      1     1.000     -.30164
C      1     2.000      .12964
C      2     1.000     -.30164     .48375
C      2     2.000      .12964    1.23679
C      9     1.000     -.30164     .48375     .94740    1.26915    1.51413
C                      1.71153    1.87667    2.01857    2.14293
C     10     1.000     -.30164     .48375     .94740    1.26915    1.51413
C                      1.71153    1.87667    2.01857    2.14293    2.25358
C     10    10.000     1.23654    2.70766    4.08107    5.36041    6.55070
C                      7.65784    8.68822    9.64829   10.54435   11.38233
C
C-----------------------------------------------------------------------      
      
      PARAMETER(KL=200)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   SIG(KL)

      IF (ETA.LT.10.D0) THEN
         ETA2=ETA*ETA
         ETA2A=2.0D0*ETA
         ETA6=ETA2+16.0D0
         SIGMA0=-(ETA/(12.D0*ETA6))*(1.D0+(ETA2-48.D0)/(30.D0*(ETA6**2))
     1          +((ETA2-160.D0)*ETA2+1280.D0)/(105.D0*(ETA6**4)))
     2          -ETA+(ETA/2.D0)*DLOG(ETA6)+3.5D0*DATAN(0.25D0*ETA)
     3          -(DATAN(ETA)+DATAN(0.5D0*ETA)+DATAN(ETA/3.D0))
      ELSE
      EINV1=1.00D0/ETA
      EINV2=EINV1*EINV1
      EINV3=EINV1*EINV2
      EINV5=EINV3*EINV2
      EINV7=EINV5*EINV2
      EINV9=EINV7*EINV2
      SIGMA0=+0.7853981634D0+ETA*DLOG(ETA)-ETA
     1       -(0.08333333333D0*EINV1+0.00277777777D0*EINV3
     2       +0.00079365079D0*EINV5+0.00059523810D0*EINV7
     3       +0.00084175084D0*EINV9)
      ENDIF
      
      MODTPI=SIGMA0/6.2831853072D0
      FMODTP=MODTPI
      SIGMA0=SIGMA0-6.2831853072D0*FMODTP

CCCCC COULOMB WAVE PHASE SHIFT

      LMAX1=LMAX-1
      SIG(1)=SIGMA0
      DO 100 L=1,LMAX1
         SS=L
         SIG(L+1)=SIG(L)+DATAN(ETA/SS)
  100 CONTINUE

      RETURN
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE POTEN(ICONT,NXMIN,NXMAX,XMES,V0,X0,A0,
     O                 VR)
C=======================================================================
C
C     PROGRAM NAME : POTEN.FOR 
C
C     PURPOSE
C     POTENTIAL ENERGY CALCULATION.
C     
C     REFERENCE
C     SAMUEL S.M.WONG, INTRODUCTORY NUCLEAR PHYSICS, (PRENTICE-HALL,
C     ENGLEWOOD CLIFFS,1990) 346-348.
C
C     INPUT DATA
C     ICONT  : INTEGER OF 1-DIGIT (TO SEE CONTROL CARD).     
C              M DENOTES THE TYPE OF THE POTENTIAL FORM.
C     NXMIN  : THE NUMBER OF MINIMUM IN INTEGRATION RANGE.
C     NXMAX  : THE NUMBER OF MAXIMUM IN INTEGRATION RANGE.
C     XMES   : MESH SIZE.
C     V0     : THE DEPTH PARAMETER OF POTENTIAL WELL.
C     X0     : THE RADIUS PARAMETER.
C     A0     : THE DIFFUSENESS PARAMETER.
C     
C     RESULT
C     VR(KX=500)  : POTENTIAL ENERGY.(nx+1)
C
C     CONTROL CARD.
C     ICONT  = 1 :  WOODS-SAXON POTENTIAL FORM.
C        VPONT = -V0/(1+EXP((X-X0)/A0))
C     ICONT  = 2 :  SPIN-ORBIT POTENTIAL FORM.
C        VPONT = -2.0*V0/A0*EXP((X-X0)/A0)/(1+EXP((X-X0)/A0))**2/X
C          REF. SAMUEL S.M. WONG, INTRODUCTORY NUCLEAR PHYSICS, 
C          (PRENTICE-HALL. INC. NEW YORK, 1990) 347.
C     ICONT  = 3 :  DERIVATIVE OF W.S. POTENTIAL FORM.
C                   (SURFACE INTERACTION.)
C        VPONT = -4.0*V0*EXP((X-X0)/A0)/(1+EXP((X-X0)/A0))**2
C     ICONT  = 4 :  COULOMB POTENTIAL FORM.
C        VPONT = V0*e**2/X0*(3-(X/X0)**2)       FOR X < X0  
C        VPONT = V0*e**2/X                      FOR X > X0 
C        V0    : THE PRODUCT OF CHARGE NUMBERS, Z1*Z2.
C        X0    : THE COULOMB RADIUS.
C     ICONT  = 5 :  GAUSSIAN POTENTIAL FORM.
C        VPONT = V0*EXP(-((X-X0)/A0)**2)
C     ICONT  = 6 :  YUKAWA POTENTIAL FORM.
C        VPONT = -V0*EXP(-(X-X0)/A0)/X
C     ICONT  = 7 :  HARMONIC OSCILLATOR POTENTIAL FORM (r**2) 
C        VPONT = V0*(X/A0)**2
C
C-----------------------------------------------------------------------      
      
      PARAMETER(KX=500)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   VR(KX)

      MAX=NXMAX
      MIN=NXMIN
      MIN1=MIN+1

CCCCC PRINT ERROR MESSAGE     
      IF (MAX.GT.KX) THEN 
	 PRINT 100, MAX 
	 STOP
      ENDIF
  100 FORMAT('  CAN NOT CALCULATE THE POTENTIAL.', 
     1     /,'  THE CALCULATION REQUIRES A LARGER DIMENSION.', 
     2     /,'  INCREASE KX, SO THAT KX > ',I5)
CCCCC

      DO 200 NX=1,KX
  200 VR(NX)=0.0D0

      GOTO (1100,1200,1300,1400,1500,1600,1700) ICONT

 1100 DO 1110 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=-V0/(1.D0+EXPX)
 1110 CONTINUE
      RETURN
 1200 V01=-2.0D0*V0/A0
      DO 1210 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=V01*EXPX/(1.0D0+EXPX)**2/XX
 1210 CONTINUE
      RETURN
 1300 DO 1310 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=-4.0D0*V0*EXPX/(1.0D0+EXPX)**2
 1310 CONTINUE
      RETURN
 1400 VCLFC2=1.439865D0*V0
      VCLFC1=0.5D0*VCLFC2/X0
      DO 1410 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 IF(XX.LT.X0) THEN
	    VR(NX)=VCLFC1*(3.0D0-(XX/X0)**2)
	 ELSE
	    VR(NX)=VCLFC2/XX
	 ENDIF
 1410 CONTINUE
      RETURN
 1500 DO 1510 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 XY=((XX-X0)/A0)**2
	 EXPX=DEXP(-XY)
	 VR(NX)=-V0*EXPX
 1510 CONTINUE
      RETURN
 1600 DO 1610 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP(-(XX-X0)/A0)
	 VR(NX)=-V0*EXPX/XX
 1610 CONTINUE
      RETURN
 1700 DO 1710 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 VR(NX)=V0*(XX/A0)**2
 1710 CONTINUE
      RETURN
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE DISWV0(L1,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,VRID,VFUS,
     O                  DWAVE,SMAT,CRFUS)           
C======================================================================
C
C     PROGRAM NAME : DISWV0.FOR
C
C     PURPOSE
C     DISTORTED WAVE, S-MATRIX AND FUSION CROSS SECTION CALCULATIONS
C     BY NUMEROV METHOD FOR A GIVEN POTENTIAL ENERGY WITHOUT SPIN-ORBIT
C     INTERACTION.
C    
C     REFERENCE
C     JOHN M.BLATT AND VICTOR F.WEISSKOPF, THEORETICAL NUCLEAR PHYSICS,
C     (WILEY, NEW YORK,1952) 331. EQ.2.41.
C      
C     REQUIRED SUBROUTINE
C     COUFT(L1,ETA,RHOMX,  FC,GC,FCP,GCP)
C     
C     INPUT DATA
C     L1     : ANGULAR MOMENTUM.(l)
C     NXMIN  : MINIMUM NUMBER OF INTEGRATION RANGE.
C     NXMAX  : MAXIMUM NUMBER OF INTEGRATION RANGE.
C     XMES   : MESH SIZE.
C     ECM    : CENTER OF MASS ENERGY.
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C     EXSGRI : EXPONENTIAL(i * COULOMB PHASE SHIFT).
C     VRID(KX=500)  :  POTENTIAL ENERGY.(nx+1)
C     VFUS(KX=500)  :  FUSION POTENTIAL ENERGY.(nx+1)
C
C     RESULTS
C     DWAVE(KX=500) : DISTORTED WAVE.(nx+1)
C     SMAT   : PARTIAL S-MATRIX.
C     CRFUS  : PARTIAL FUSION CROSS SECTION.
C
C----------------------------------------------------------------------
      
      PARAMETER(KX=500)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   VRID(KX),VFUS(KX),DWAVE(KX)
      COMPLEX*16     EXSGRI,VRID,DWAVE,SMAT
      COMPLEX*16     URI1,URI2,URI1H,URI2H,URI3H  
      COMPLEX*16     ZERO,TTI,CON1,B1,CAB,D,CL,COR
      COMPLEX*16     VRIT,ARI,BRI

      NXMX2=NXMAX+2
      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D0)
      RM=XMES*DFLOAT(NXMAX-1)
      TMU=WN**2/ECM
      HSQ=XMES**2
      HSQ12=HSQ/12.0D0
      YMG=1.0D-90
      YMP1=-2.0D-90

      IF (NXMX2.GT.KX) THEN
         PRINT 50, NXMAX,KX
         STOP
      ENDIF
   50 FORMAT(1H ,'NXMAX,KX=',2I5,/
     1       1H ,'INCREASE KX IN PARAMETER')

      DO 100 NX=1,NXMX2
         DWAVE(NX)=ZERO
  100 CONTINUE

      RHOMX=RM*WN

      CALL COUFT(L1,ETA,RHOMX,FC,GC,FCP,GCP) 

      FC2D=FC
      GC2D=GC
      FCPM=FCP*WN
      GCPM=GCP*WN

      URI1H=ZERO
      URI2H=YMG
      IF (L1.EQ.1) URI2H=YMP1

      NXMN1=NXMIN+1
      DO 200 NX=NXMN1,NXMX2
         CFFCLL=L1*(L1+1)
         XX=DFLOAT(NX-1)*XMES
         XSQIN=1.0D0/(XX*XX)
         VRIT=-(ECM-VRID(NX))*TMU 
         ARI=VRIT+CFFCLL*XSQIN
         BRI=1.0D0-HSQ12*ARI
         DWAVE(NX)=URI2H/BRI
         URI3H=(2.0D0+(HSQ*ARI/BRI))*URI2H-URI1H
         URI1H=URI2H
         URI2H=URI3H
  200 CONTINUE

      N5=NXMAX-3
      DENOM=1.0D0/(12.0D0*XMES)
      URI1=DWAVE(N5+3)
      URI2=(8.0D0*(DWAVE(N5+4)-DWAVE(N5+2)) 
     1          -DWAVE(N5+5)+DWAVE(N5+1))*DENOM
      CON1=-URI2/URI1
      B1=CON1*FC2D+FCPM
      CAB=-CON1*(GC2D+TTI*FC2D)
      D=-GCPM-TTI*FCPM
      CL=B1/(CAB+D)
      CON1=FC2D+CL*(GC2D+TTI*FC2D)
      COR=EXSGRI*CON1/URI1
      SMAT=1.D0+2.D0*TTI*CL

      DO 300 NX=NXMN1,NXMX2
         DWAVE(NX)=DWAVE(NX)*COR
  300 CONTINUE

      CRFUS=0.0
      DO 400 NX=NXMIN,NXMAX
         CRFUS=CRFUS-DWAVE(NX)*DCONJG(DWAVE(NX))*VFUS(NX)*XMES
  400 CONTINUE

      RETURN
      END 
CCCCCC
CCCCCC
C========================================================================
      SUBROUTINE COUFT(L1,ETA,RHO,
     O                 F,G,FP,GP)
C========================================================================
C
C     PROGRAM NAME : COUFT.FOR
C
C     PURPOSE
C     CALCULATIONS OF PARTIAL COULOMB WAVE FUNCTIONS FOR A GIVEN ETA, 
C     RHO AND ANGULAR MOMENTUM.
C
C     REFERENCE
C     A.R.BARNET, D.H.FENG, J.W.STEED AND L.J.B.GOLDFARB,
C     COMPUTER PHYS. COMMUNICATIONS 8 (1974) 377.
C
C     INPUT DATA 
C     L1     : ANGULAR MOMENTUM.(l)
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v)
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     RHO    = k*r = sqrt(mu*Ecm)*r/h_BAR
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C
C     RESULTS
C     F      : REGULAR COULOMB WAVE FUNCTION AT RHO.(l)
C     FP     : DERIVATIVE OF F AT RHO.(l)
C     G      : IRREGULAR COULOMB WAVE FUNCTION AT RHO.(l)
C     GP     : DERIVATIVE OF G AT RHO.(l)
C
C     EXAMPLE
C     L1    ETA    RHOMX       F           G            FP           GP
C     1    1.000   1.000   7.5182E-02  3.78273E+00  1.7142E-01  -4.67592E+00    
C     1    2.000   2.000   8.1166E-02  5.06994E+00  1.2006E-01  -4.82089E+00   
C     2    1.000   1.000   1.4785E-02  1.26415E+01  4.7087E-02  -2.73742E+01   
C     2    2.000   2.000   2.9892E-02  1.05780E+01  5.5013E-02  -1.39859E+01   
C    10   10.000   9.000   1.6896E-06  1.84188E+05  2.8045E-06  -2.86121E+05    
C    10   10.000  10.000   8.1838E-06  4.21596E+04  1.2269E-05  -5.89840E+04    
C
C--------------------------------------------------------------------------      
      
	IMPLICIT REAL*8(A-H,O-Z)
      REAL        K,K1,K2,K3,K4,M1,M2,M3,M4

      MINL=L1
      MAXL=L1
      ACCUR=1.0D-10
      STEP=999.0D0
      PACE=STEP
      ACC=ACCUR
      IF(PACE.LT.100.0D0)PACE=100.0D0
      IF(ACC.LT.1.0D-15.OR.ACC.GT.1.0D-6) ACC = 1.0D-6
      R=RHO
      KTR=1
      LMAX=MAXL
      LMIN1=MINL+1
      XLL1=DFLOAT(MINL*LMIN1)
      ETA2=ETA*ETA
      TURN=ETA+DSQRT(ETA2+XLL1)
      IF(R.LT.TURN.AND.DABS(ETA).GE.1.0D-6)KTR=-1
      KTRP=KTR
      GOTO 2
1     R=TURN
      TF=F
      TFP=FP
      LMAX=MINL
      KTRP=1
2     ETAR=ETA*R
      RHO2=R*R
      PL=DFLOAT(LMAX+1)
      PMX=PL+0.5
      FP=ETA/PL+PL/R
      DK=ETAR*2.00D0
      DEL=0.0D0
      D=0.0D0
      F=1.0D0
      K=(PL*PL-PL+ETAR)*(2.0D0*PL-1.0D0)
      IF(PL*PL+PL+ETAR.NE.0.0D0)GOTO3
      R=R+1.0D-6
      GOTO 2
3     H=(PL*PL+ETA2)*(1.0D0-PL*PL)*RHO2
      K=K+DK+PL*PL*6.0D0
      D=1.0D0/(D*H+K)
      DEL=DEL*(D*K-1.0D0)
      IF(PL.LT.PMX)DEL=-R*(PL*PL+ETA2)*(PL+1.0D0)*D/PL
      PL=PL+1.0D0
      FP=FP+DEL
      IF(D.LT.0.0D0)F=-F
      IF(PL.GT.20000.D0) GOTO 11
      IF(DABS(DEL/FP).GE.ACC) GOTO 3
      FP=F*FP
      IF(LMAX.EQ.MINL) GOTO 5
CC
5     IF(KTRP.EQ.-1) GOTO 1
      P=0.0D0
      Q=R-ETA
      PL=0.0D0
      AR=-(ETA2+XLL1)
      AI=ETA
      BR=2.0D0*Q
      BI=2.0D0
      WI=2.0D0*ETA
      DR=BR/(BR*BR+BI*BI)
      DI=-BI/(BR*BR+BI*BI)
      DP=-(AR*DI+AI*DR)
      DQ=(AR*DR-AI*DI)
6     P=P+DP
      Q=Q+DQ
      PL=PL+2.0D0
      AR=AR+PL
      AI=AI+WI
      BI=BI+2.0D0
      D=AR*DR-AI*DI+BR
      DI=AI*DR+AR*DI+BI
      T=1.0D0/(D*D+DI*DI)
      DR=T*D
      DI=-T*DI
      H=BR*DR-BI*DI-1.0D0
      K=BI*DR+BR*DI
      T=DP*H-DQ*K
      DQ=DP*K+DQ*H
      DP=T
      IF(PL.GT.46000.D0) GOTO 11
      IF(DABS(DP)+DABS(DQ).GE.(DABS(P)+DABS(Q))*ACC)GOTO6
      P=P/R
      Q=Q/R
      G=(FP-P*F)/Q
      GP=P*G-Q*F
      W=1.0D0/DSQRT(FP*G-F*GP)
      G=W*G
      GP=W*GP
      IF(KTR.EQ.1) GOTO 8
      F=TF
      FP=TFP
      LMAX=MAXL
      IF(RHO.LT.0.2*TURN)PACE=999.0D0
      R3=1.0D0/3.0D0
      H=(RHO-TURN)/(PACE+1.0D0)
      H2=0.5D0*H
      I2=INT(PACE+0.001D0)
      ETAH=ETA*H
      H2LL=H2*XLL1
      S=(ETAH+H2LL/R)/R-H2
7     RH2=R+H2
      T=(ETAH+H2LL/RH2)/RH2-H2
      K1=H2*GP
      M1=S*G
      K2=H2*(GP+M1)
      M2=T*(G+K1)
      K3=H*(GP+M2)
      M3=T*(G+K2)
      M3=M3+M3
      K4=H2*(GP+M3)
      RH=R+H
      S=(ETAH+H2LL/RH)/RH-H2
      M4=S*(G+K3)
      G=G+(K1+K2+K3+K2+K4)*R3
      GP=GP+(M1+M2+M2+M3+M4)*R3
      IF(( G.GT.1.0D65 ).OR.( GP.GT.1.0D65 ))GOTO11
C      IF(( G.GT.1.0D36 ).OR.( GP.GT.1.0D36 ))GOTO11
      R=RH
      I2=I2-1
      IF(I2.GE.0) GOTO 7
      W=1.0D0/(FP*G-F*GP)
    8 F=W*F
      FP=W*FP
      
  500 RETURN

   11 W=0.0D0
      G=0.0D0
      GP=0.0D0
      GOTO 8
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE ELCROS0(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
     O                  CRRUTH,CREL,CRATIO)
C=======================================================================
C
C     PROGRAM NAME : ELCROS0.FOR
C
C     PURPOSE
C     RUTHERFORD, ELASTIC SCATTERING CROSS SECTION AND RATIO
C     CALCULATIONS WITHOUT SPIN-ORBIT INTERACTION.
C
C     REQUIRED SUBROUTINE
C     LEGNL0(TH,LMAX,  PK)
C     
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     NAMAX  : MAXIMUM NUMBER OF ANGLE.
C     ECM    : CENTER OF MASS ENERGY.
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS k THE WAVE NUMBER.
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1) 
C     SMAT(KL=200)  : S-MATRIX.(l+1)
C     THETAD(KA=180): THE ANGLE IN THE UNIT OF DEGREE.(na)
C
C     RESULTS
C     CRRUTH(KA=180): RUTHERFORD SCATTERING CROSS SECTION.(na)
C     CREL(KA=180)  : ELASTIC SCATTERING CROSS SECTION.(na)
C     CRATIO(KA=180): RATIO OF ELASTIC TO RUTHERFORD SCATTERING CROSS
C                     SECTION.(na)
C
C------------------------------------------------------------------------
      
      PARAMETER(IW=6,KL=200,KA=180)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   SIG(KL),THETAD(KA),PK(KL),
     1            CRRUTH(KA),CREL(KA),CRATIO(KA)
      DIMENSION   SMAT(KL),CL(KL) 
      COMPLEX*16     ZERO,TTI,FN,FNPC,FCC,CPH,EXPSIG,SMAT,CL

      PI=4.0D0*DATAN(1.0D00)
      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D00)

      DO 100 L1=1,LMAX
         CL(L1)=(SMAT(L1)-1.D0)/(2.0D0*TTI)
  100 CONTINUE 

      DO 300 NA=1,NAMAX
         TH=THETAD(NA)*PI/180.0D0

         CALL LEGNL0(TH,LMAX,  PK)
                                    
         FN=ZERO
         DO 200 L1=1,LMAX
            L=L1-1
            L21=2*L+1
            EXPSIG=CDEXP(2.D00*TTI*SIG(L1))
            FN=FN+DFLOAT(L21)*EXPSIG*CL(L1)*PK(L1)
  200   CONTINUE

        FCC=0.0D00
        IF (ETA.NE.0.0) THEN
           SIN2=(DSIN(0.5D00*TH))**2
           FC0=ETA/(2.D00*WN*SIN2)
           CPH=DCMPLX(0.D00,-ETA*DLOG(SIN2)+2.D00*SIG(1))
           FCC=FC0*CDEXP(CPH)
        ENDIF
        FNPC=FN/WN-FCC
        CREL(NA)  =10.D0*FNPC*CONJG(FNPC)
        CRRUTH(NA)=10.D0*FC0**2
        IF (ETA.EQ.0.0D0) CRRUTH(NA)=1.0D0
        CRATIO(NA)=CREL(NA)/CRRUTH(NA)
  300 CONTINUE

      WRITE(IW,900)
      DO 500 NA=1,NAMAX
         WRITE(IW,910) THETAD(NA),CREL(NA),CRRUTH(NA),CRATIO(NA),
     1                 THETAD(NA)
  500 CONTINUE
  900 FORMAT(//,'   ANGLE  ELASTIC CS    RUTH. CS       RATIO  ANGLE'/,
     1          '   (DEG)      (MB)        (MB)                (DEG)'/)
  910 FORMAT(1X,F7.2,3E12.3,F7.2)

      RETURN
      END
CCCCCC   
CCCCCC
C=====================================================================C              
      SUBROUTINE LEGNL0(TH,LMAX,
     O                  PK)
C=====================================================================C              
C
C     PROGRAM NAME : LEGNL0.FOR
C
C     PURPOSE
C     LEGENDRE FUNCTION CALCULATION AT GIVEN TH AND LMAX. 
C
C     REFERENCE
C     MILTON ABRAMOWITZ AND IRENE A.STEGUN, HANDBOOK OF MATHEMATICAL
C     FUNCTIONS WITH FORMULAS, GRAPHS, AND MATHEMATICAL TABLES,
C     (NATIONAL BUREAU OF STANDARDS, WASHINGTON 1964) 334. EQ.8.5.3.
C     
C     INPUT DATA
C     TH     : THE ANGLE IN THE UNIT OF RADIAN.
C     LMAX     : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C
C     RESULTS
C     PK(KL=200)   : TO CACULATE IN Z-PROJECTION TO "0".(l+1)
C
C      EXAMPLE
C       TH    LMAX   PK(1/6        2/7        3/8        4/9       5/10)
C      1.000    1    1.00000
C      1.000    2    1.00000     .54030
C      2.000    2    1.00000    -.41614
C      1.000   10    1.00000     .54030    -.06211    -.41613    -.34688
C                   -0.00445     .28465     .28944     .04415    -.21222
C      2.000   10    1.00000    -.41614    -.24023     .44405    -.14320
C                   -0.24796     .30852    -.02589    -.24975     .21933
C
C-----------------------------------------------------------------------

      PARAMETER( KL=200 )      
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   PK(KL)

      IF(LMAX.GE.KL) THEN
         PRINT 500,LMAX
      END IF
  500 FORMAT('  CAN NOT CALCULATE THE POTENTIAL.',
     1     /,'  THE CALCULATION REQUIRES A LAGER DIMENSION.',
     2     /,'  INCREASE KL, SO THAT KL >',I5)

      PK(1)=1.0D0
      PK(2)=DCOS(TH)

      DO 100 L=3,LMAX
         S=DFLOAT(L)
         PK(L)=(1.D0/(S-1.D0))*((2.D0*S-3.D0)
     1            *DCOS(TH)*PK(L-1)-(S-2.D0)*PK(L-2))
  100 CONTINUE                                                                 
      RETURN
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE ELETC0(LMAX,WN,ETA,SMAT,FUST,
     O                  FCROS,RECROSS)
C======================================================================
C
C     PROGRAM NAME : ELETC0.FOR
C
C     PURPOSE
C     TOTAL FUSION AND REACTION CROSS SECTION CALCULATIONS FOR GIVEN
C     S-MATRICES AND PARTIAL FUSION CROSS SECTIONS WITHOUT SPIN-ORBIT
C     INTERACTION.
C
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C     SMAT(KL=200)  : S-MATRIX.(l+1)
C     FUST(KL=200)  : FUSION CROSS SECTION.(l+1)
C
C     RESULTS
C     FCROS   : TOTAL FUSION CROSS SECTION.
C     RECROSS : TOTAL REACTION CROSS SECTION.
C
C------------------------------------------------------------------------
      
      PARAMETER(IW=6,KL=200)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   FUST(KL),ABSS(KL),ABSC(KL),PHAS(KL),PHAC(KL)
      DIMENSION   TCOE(KL),RECROS(KL),TCROS(KL)
      DIMENSION   SMAT(KL),CL(KL)
      COMPLEX*16     TTI,SMAT,CL,SMM1

      TTI=(0.0D0,1.0D0)
      PI=4.0D0*DATAN(1.0D00)

      DO 10 L=1,LMAX
      ABSS(L)=0.0D0
      ABSC(L)=0.0D0
      PHAS(L)=0.0D0
      PHAC(L)=0.0D0
   10 CONTINUE

      FCROS=0.0D0
      FCRO1=0.0D0
      FCRO2=0.0D0
      DO 100 L=1,LMAX
         FL=DFLOAT(L-1)
         FCROS=FCROS+FUST(L)
         FCRO1=FCRO1+FUST(L)*FL
         FCRO2=FCRO2+FUST(L)*FL**2
  100 CONTINUE
         AVEL1=FCRO1/FCROS
         AVEL2=FCRO2/FCROS
         AVEL2=DSQRT(AVEL2)

      TOCROSS=0.0D0
      RECROSS=0.0D0
      ELCROSS=0.0D0
      AREA=PI/(WN*WN)
      DO 200 L=1,LMAX
         LL=L-1
         FL1=DFLOAT(LL*2+1)
         CL(L)    =(SMAT(L)-1.D0)/(2.D0*TTI)
         TCOE(L)  =1.D0-SMAT(L)*CONJG(SMAT(L))
         RECROS(L)=10.D0*TCOE(L)*FL1*AREA
         RECROSS=RECROSS+RECROS(L)
         IF (ETA.EQ.0.0D0) THEN
            SMM1=1.0D0-SMAT(L)
            SMM12=SMM1*CONJG(SMM1)
            TCROS(L)=10.0D0*(1.0D0-REAL(SMAT(L)))*FL1*AREA*2.0
            TOCROSS=TOCROSS+TCROS(L)
            ELCROSS=ELCROSS+10.0D0*SMM12*FL1*AREA
         ENDIF 
  200 CONTINUE

      DO 300 L=1,LMAX
         X1=REAL(SMAT(L))
         X2=AIMAG(SMAT(L))
         ABSM=(X1**2)+(X2**2)
         IF (ABSM.LE.1.D-20) GO TO 300
         ABSS(L)=DSQRT(ABSM)
         PHAS(L)=DATAN2(X2,X1)
         X1=REAL(CL(L))
         X2=AIMAG(CL(L))
         ABSM=(X1**2)+(X2**2)
         IF (ABSM.LE.1.D-20) GO TO 300
         ABSC(L)=DSQRT(ABSM)
         PHAC(L)=DATAN2(X2,X1)
  300 CONTINUE

      LH=0     
      DO 400 L=1,LMAX
         IF (TCOE(L).GE.0.5D0) LH = L 
  400 CONTINUE
      LSA=LH-1
      HL=DFLOAT(LH)-0.5D0
      IF (ETA.NE.0.0D0) THEN
         A=1.0D0+(HL/ETA)**2
         SAR=(ETA/WN)*(1.0D0+DSQRT(A))
      ELSE
      SAR=HL/WN
      ENDIF

      IF (ETA.EQ.0.0D0) THEN
         WRITE(IW,900)
      ELSE
      WRITE(IW,902) 
      ENDIF

      DO 500 L=1,LMAX
         L1=L-1
         IF (ETA.EQ.0.0D0) THEN
            WRITE(IW,910) L1,ABSS(L),PHAS(L),TCOE(L),
     1                    FUST(L),RECROS(L),TCROS(L),L1
         ELSE
         WRITE(IW,912) L1,ABSS(L),PHAS(L),ABSC(L),PHAC(L),TCOE(L),
     1                 FUST(L),RECROS(L),L1
         ENDIF
  500 CONTINUE
      IF (ETA.EQ.0.0D0) THEN
         WRITE(IW,922) FCROS,RECROSS,TOCROSS
         WRITE(IW,930) ELCROSS
      ELSE
      WRITE(IW,920) FCROS,RECROSS
      ENDIF
      WRITE(IW,940) AVEL1
      WRITE(IW,950) AVEL2
      WRITE(IW,960) LSA
      WRITE(IW,970) SAR

  900 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
     1          1H ,' L',' ABS S-MAT',' PHA S-MAT',
     2          '   T-COEFF',4X,'FUSION','  REACTION','     TOTAL',  
     3          '   L'/17X,'(RAD)',17X,'(MB)',6X,'(MB)',6X,'(MB)'/)
  902 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
     1          1H ,' L',' ABS S-MAT',' PHA S-MAT',' ABS C-MAT',
     2          ' PHA C-MAT','   T-COEFF',4X,'FUSION','  REACTION'
     3          ,'   L'/17X,'(RAD)',15X,'(RAD)',17X,'(MB)',6X,'(MB)'/)
  910 FORMAT(I3,6E10.3,I4)
  912 FORMAT(I3,7E10.3,I4)
  920 FORMAT(1H ,'TOTAL'47X,3E10.3)
  922 FORMAT(1H ,'TOTAL'27X,3E10.3)
  930 FORMAT(1H ,'TOTAL ELASTIC CROSS SECTION',25X,E10.3)
  940 FORMAT(1H ,'AVERAGE L-VALUE         =',E10.3)
  950 FORMAT(1H ,'AVERAGE L-SQUARE ROOT   =',E10.3)
  960 FORMAT(1H ,'STRONG ABSORPTION L     =',I5)
  970 FORMAT(1H ,'STRONG ABSORPTION RADIUS=',E10.3)
 
      RETURN
      END
