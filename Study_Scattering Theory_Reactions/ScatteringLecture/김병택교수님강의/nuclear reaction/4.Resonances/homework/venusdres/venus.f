    
      PROGRAM VENUS
	IMPLICIT REAL*8(A-H,O-Z)
      PARAMETER(KK=10,KA=180,KL=200,KL2=KL*2,KE=100)
C     
      DIMENSION   KTRL(KK),KOUT(KK)
      DIMENSION   THETAD(KA)
	DIMENSION   SIGLS(KL2),PHAS(KL2)
	DIMENSION   ELE(KE),ECE(KE),SIGLSE(KE,KL2),PHASE(KE,KL2)
C				
      OPEN(5,FILE='venus.DAT',STATUS='OLD')
      OPEN(6,FILE='venus.OUT',STATUS='OLD')
C						   
C     ***************     READ INITIAL DATA     ***************
C							       
   10 FORMAT(24I3)
   11 FORMAT(14I5)
   15 FORMAT(10F7.4)
C		    
      READ(5,10) (KTRL(N),N=1,10)
      READ(5,10) (KOUT(N),N=1,10)
      READ(5,15) TMST,TZ,PMST,PZ
      READ(5,15) ELABI,DELABI,XMES,THMIN,THMAX,THINC
      READ(5,11) LMAX,NXMX,NELABI
      READ(5,15) VO,WO,WS1,AO,AI,AIS1,RO,RI,RIS1,RC
      READ(5,15) WS2,AIS2,RIS2,VSP,ASP,RSP
C    
      THETA=THMIN-THINC
      NOTH=(THMAX-THMIN)/THINC+1.0001
      DO 400 K=1,NOTH
      THETA=THETA+THINC
      THETAD(K)=THETA
  400 CONTINUE
C
      JMAX=2*LMAX-1
      DO 600 NE=1,NELABI
	ELAB=ELABI+DELABI*DFLOAT(NE-1)      
C======================================================================
      CALL OPTMDL(NE,KTRL,KOUT,LMAX,NXMX,NOTH,
     1             TMST,PMST,TZ,PZ,ELAB,XMES,THETAD,
     2             VO,WO,WS1,WS2,AO,AI,AIS1,AIS2,RO,RI,RIS1,RIS2,RC,
     3             VSP,ASP,RSP,SIGLS,PHAS,ECM)
C======================================================================
      ELE(NE)=ELAB
	ECE(NE)=ECM

      DO 500 L=1,JMAX
         L1=L/2
         J2=L
         LMODE=MOD(L,2)
         IF (LMODE.EQ.0) J2=L-1
	   PHASE(NE,L)=PHAS(L)
	   SIGLSE(NE,L)=SIGLS(L)
  500 CONTINUE

  600 CONTINUE

      DO 700 NE=1,NELABI
      WRITE(6,650) ELE(NE),ECE(NE),(PHASE(NE,L),L=1,JMAX)
  650 FORMAT(10E12.3)
  700 CONTINUE

      DO 710 NE=1,NELABI
      WRITE(6,650) ELE(NE),ECE(NE),(SIGLSE(NE,L),L=1,JMAX)
  710 CONTINUE
C
      STOP
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE OPTMDL(NE,KTRL,KOUT,LMAX,NXMAX,NAMAX,TMST,PMST,TZ,PZ,
     1           ELABI,XMES,THETAD,VO,WO,WS1,WS2,AO,AI,AIS1,AIS2,
     2           RO,RI,RIS1,RIS2,RC,VSP,ASP,RSP,SIGLS,PHAS,ECM)
C======================================================================
C
C     PROGRAM NAME : OPTMDL.FOR 
C
C     PURPOSE
C     DISTORTED WAVE, COULOMB PHASE SHIFT, AND S-MATRICES CALCULATIONS 
C     FOR OPTICAL MODEL PARAMETERS CONSIDERING OPTICAL MODEL WITH 
C     SPIN-ORBIT INTERACTION. 
C
C     REQUIRED SUBROUTINE
C     COUPHS(LMAX,ETA,  SIG)
C     POTEN(ICONT,NXMIN,NXMAX,XMES,VO,R0,AO,  VPOT)
C     DISWV1(LP,J2,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,VRID,VSPIN,VFUS,
C    O       DWAVE,SM,OVEW)
C     ELCROS1(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
C    O        CRRUTH,CREL,CRATIO)
C     ELETC1(LMAX,WN,ETA,SMAT,FUST,  FCROS,TCROS)
C
C     INPUT DATA 
C     KTRL(KK=10)   : REACTION CONTROL CARD.
C     KOUT(KK=10)   : OUTPUT CONTROL CARD.
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTOM.(l+1)
C     NXMAX  : MAXIMUM NUMBER OF INTEGRATION RANGE.
C     NAMAX  : MAXIMUM NUMBER OF ANGLE.
C     TMST   : TARGET MASS.
C     PMST   : PROJECTIL MASS.
C     TZ     : TARGET CHARGE.
C     PZ     : PROJECTIL CHARGE.
C     ELABI  : LABORATORY SYSTEM ENERGY.
C     XMES   : MESH SIZE.
C     THETAD(KA=180): THE ANGLE IN THE UNIT OF DEGREE.(na)
C     VO, AO,  RO   : OPTICAL MODEL PARAMETER REAL PART.
C     WO, AI,  RI   : OPTICAL MODEL PARAMETER IMAGINARY PART.
C     WS1,AIS1,RIS1 : SURFACE INTERACTION PARAMETER IMAGINARY PART 1.
C     WS2,AIS2,RIS2 : SURFACE INTERACTION PARAMETER IMAGINARY PART 2.
C     RC     : COULOMB RADIUS PARAMETER.
C     VSP,ASP,RSP   : SPIN INTERACTION PARAMETER.
C
C     RESULT
C     DWAVE(KX=500) : DISTORTED WAVE.(nx+1)
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1)
C     SMAT(KL=KL*2) : S-MATRIX, CONSIDER SPIN DOWN AND UP.(l+j+s)
C
C-----------------------------------------------------------------------
C      
      PARAMETER(IW=6,KK=10,KL=200,KX=500,KA=180,KL2=KL*2)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   KTRL(KK),KOUT(KK)
      DIMENSION   SIG(KL),FUST(KL2),SMAT(KL2)
	DIMENSION   SIGLS(KL2),PHAS(KL2)
      DIMENSION   VREAL(KX),VREPC(KX),WVOL(KX),WSUR1(KX),WSUR2(KX)
      DIMENSION   VSPIN(KX),VFUS(KX),DWAVE(KX),VRID(KX)
      DIMENSION   THETAD(KA),CRRUTH(KA),CREL(KA),CRATIO(KA)
      COMPLEX*16     ZERO,TTI,EXSGRI,SM,SMAT,DWAVE,VRID
C
      KTRL1=KTRL(1)      !
      KTRL2=KTRL(2)      !=0, VOLUME IMAGINARY POT. ONLY FOR FUSION.
C                        !=1, VOLUME+SURFACE1 TERM ONLY.
C                        !=2, VOLUME+SURFACE1+SURFACE2.
      KTRL3=KTRL(3)      !=1, ELASTIC CROSS SECTIONS ARE CALCULATED.
      KTRL4=KTRL(4)      !=1, FUSION AND PARTIAL WAVE INFOR. ARE CALCULATED.
C 
      KOUT1=KOUT(1)      !KINEMATICAL VALUES.
      KOUT2=KOUT(2)      !POTENTIAL VALUES.
C
      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D0)
C
      HBAR=197.327053D0
      AMU =931.49432D0
      FINE=137.0359895D0
      PI  =4.0D0*DATAN(1.0D00)
      WNUNIT=DSQRT(2.0D0*AMU)/HBAR
      ESQ   =HBAR/FINE
      HBRSQ =HBAR*HBAR
C
      TMPM=TMST**0.3333333333+PMST**0.3333333333
      IF (PMST.LT.4.0) TMPM=TMST**0.3333333333
      RMST=TMST*PMST/(TMST+PMST)
      TZPZ=TZ*PZ
      ELAB=ELABI
      ECM =ELAB*TMST/(TMST+PMST)
      WN  =WNUNIT*DSQRT(ECM*RMST)
      ETA =RMST*AMU*TZPZ*ESQ/(WN*HBRSQ)
C
      FAC1=40.0D0*PI*RMST*WNUNIT**2/WN**3
      FAC2=40.0D0*PI/WN**2
C
      IF (KOUT1.NE.0) THEN
	IF (NE.EQ.1) THEN
	 WRITE(IW,700) ELABI
	 WRITE(IW,710) TMST,TZ,PMST,PZ
	 WRITE(IW,720)
	 WRITE(IW,722) (KTRL(N),N=1,10)
	 WRITE(IW,724) (KOUT(N),N=1,10)
	 WRITE(IW,730)
	 WRITE(IW,732) VO,AO,RO
	 WRITE(IW,734) WO,AI,RI
	 WRITE(IW,736) WS1,AIS1,RIS1
	 WRITE(IW,738) WS2,AIS2,RIS2
	 WRITE(IW,739) VSP,ASP,RSP
	 WRITE(IW,740) RC
	 WRITE(IW,750) LMAX
	 WRITE(IW,752) NXMAX,XMES
	 WRITE(IW,754) NAMAX,THETAD(1),THETAD(NAMAX)
	ENDIF
	 WRITE(IW,760) ELABI
	 WRITE(IW,762) ECM
	 WRITE(IW,764) WN
	 WRITE(IW,766) ETA
      ENDIF
  700 FORMAT(1H1,/,1H ,'ELASTIC SCATTERING CROSS SECTIONS AT ELAB='
     1       F9.3)
  710 FORMAT(1H ,5X,'TARGET     MASS=',F8.2,' CHARGE=',F8.2/
     1       1H ,5X,'PROJECTILE MASS=',F8.2,' CHARGE=',F8.2//)
  720 FORMAT(1H ,'     N =  1  2  3  4  5  6  7  8  9 10 ')
  722 FORMAT(1H ,'KTRL(N)=',10I3)
  724 FORMAT(1H ,'KOUT(N)=',10I3)
  730 FORMAT(//,1H ,'OPTICAL MODEL PARAMETERS',/,
     1       14X,' DEPTH      DIFF    RADIUS')
  732 FORMAT(1H ,'REAL      =',3F10.5)
  734 FORMAT(1H ,'IMAGINARY =',3F10.5)
  736 FORMAT(1H ,'SUR.IMAG1 =',3F10.5)
  738 FORMAT(1H ,'SUR.IMAG2 =',3F10.5)
  739 FORMAT(1H ,'SPIN-ORBIT=',3F10.5)
  740 FORMAT(1H ,'COULOMB   =',20X,F10.5)
  750 FORMAT(//,1H ,'LMAX             =',I6)
  752 FORMAT(   1H ,'NXMAX,XMES       =',I6,2F9.3)
  754 FORMAT(   1H ,'NAMAX,THMIN,THMAX=',I6,2F9.3)
  760 FORMAT(//,1H ,'ELAB  =',F9.3)
  762 FORMAT(   1H ,'ECM   =',F9.3)
  764 FORMAT(   1H ,'WN    =',F9.3)
  766 FORMAT(   1H ,'ETA   =',F9.3)

C**********************************************************************
CHECKING INPUT DATA
C      RMAX=AMAX1(RO,RI,RIS1,RIS2,RSP,RC)
C      RMAX=RMAX*TMPM      
C      AMAX=AMAX1(AO,AI,AIS1,AIS2,ASP)      
C      AMIN=AMIN1(AO,AI,AIS1,AIS2,ASP)      
C      RMAX=2.2*AMAX+RMAX+1.0
C      NXCHK =RMAX/XMES
C      LCHK  =RMAX*WN
C      XTAMES=AMIN/5.0   
C      XTKMES=2.*PI/(WN*10.0)
C      XCHK  =AMIN1(XTAMES,XTKMES)
C      NACHK =0
C      IF (THETAD(1).EQ.0.0.OR.THETAD(NAMAX).EQ.180.0) NACHK=1
C
C      IF (LCHK.GE.LMAX)   WRITE(IW,70) LMAX,LCHK
C      IF (NXCHK.GE.NXMAX) WRITE(IW,71) NXMAX,NXCHK
C      IF (XCHK.LT.XMES)   WRITE(IW,72) XMES,XCHK
C      IF (ETA.NE.0.0.AND.NACHK.EQ.1) WRITE(IW,73) 
C   70 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR LMAX =',I5,'   SUGGESTED VALUE=',I5)
C   71 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR NXMAX=',I5,'   SUGGESTED VALUE=',I5)
C   72 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR XMES =',F10.5,'   SUGGESTED VALUE=',F10.5)
C   73 FORMAT(1H ,'WARNING WARNING WARNING ON YOUR INPUT DATA'/
C     1           'YOUR ANGLE IS EITHER 0.0 OR 180.0.')
C********************************************************************
C
      CALL COUPHS(LMAX,ETA,SIG)
C
      ICONT=1
      NXMIN=1
      R0=RO*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,VO,R0,AO,VREAL)
      R0=RI*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WO,R0,AI,WVOL)
      ICONT=2
      R0=RSP*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,VSP,R0,ASP,VSPIN)
      ICONT=3
      R0=RIS1*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WS1,R0,AIS1,WSUR1)
      R0=RIS2*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,WS2,R0,AIS2,WSUR2)
      ICONT=4
      V0=TZPZ
      R0=RC*TMPM
      CALL POTEN(ICONT,NXMIN,NXMAX,XMES,V0,R0,AO,VREPC)

      DO 100 NX=NXMIN,NXMAX
	 VRID(NX)=VREAL(NX)+VREPC(NX)
     1           +TTI*(WVOL(NX)+WSUR1(NX)+WSUR2(NX))
	 VFUS(NX)=WVOL(NX)
  100 CONTINUE
C      
      IF (KTRL2.EQ.1) THEN
	 DO 110 NX=NXMIN,NXMAX
	    VFUS(NX)=WVOL(NX)+WSUR1(NX)
  110    CONTINUE
      ENDIF
C
      IF (KTRL2.EQ.2) THEN
	 DO 120 NX=NXMIN,NXMAX
	    VFUS(NX)=WVOL(NX)+WSUR1(NX)+WSUR2(NX)
  120    CONTINUE
      ENDIF
C
      IF (KOUT2.NE.0) THEN
	IF (NE.EQ.1) THEN
	 XMIN=DFLOAT(NXMIN-1)*XMES
	 XMAX=DFLOAT(NXMAX-1)*XMES
	 XINC=DFLOAT(KOUT2)*XMES
	 WRITE(IW,800) XMIN,XMAX,XINC
	 WRITE(IW,810)
	 WRITE(IW,812) (VREAL(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,820)
	 WRITE(IW,812) (VREPC(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,830)
	 WRITE(IW,812) (WVOL(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,840)
	 WRITE(IW,812) (WSUR1(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,850)
	 WRITE(IW,812) (WSUR2(NX),NX=NXMIN,NXMAX,KOUT2)
	 WRITE(IW,860)
	 WRITE(IW,812) (VSPIN(NX),NX=NXMIN,NXMAX,KOUT2)
	ENDIF
      ENDIF
  800 FORMAT(//,1H ,'OPTICAL POTENTIAL FROM',F8.3,' TO',F8.3,
     1              ' IN STEPS OF',F10.4)
  810 FORMAT(/,1H ,' REAL PART')
  812 FORMAT(  1H ,5E12.5)
  820 FORMAT(/,1H ,' COULOMB PART')
  830 FORMAT(/,1H ,' IMAGINARY VOLUME PART')
  840 FORMAT(/,1H ,' IMAGINARY SURFACE1 PART')
  850 FORMAT(/,1H ,' IMAGINARY SURFACE2 PART')
  860 FORMAT(/,1H ,' SPIN-ORBIT PART')

      DO 200 L=1,LMAX
	 FUST(L)=0.0
	 SMAT(L)=ZERO
  200 CONTINUE

      JLS=0
      DO 300 LP1=1,LMAX
	 LP=LP1-1
	 FL2=DFLOAT(2*LP+1)
	 EXSGRI=CDEXP(TTI*SIG(LP1))

	 DO 250 J1=1,3,2
	    J2=LP*2+(J1-2)
	    FJ2=DFLOAT(J2+1)
	    IF (J2.LE.0) GO TO 250
	    JLS=JLS+1             
	    
	    CALL DISWV(LP,J2,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,
     1                VRID,VSPIN,VFUS,
     2                DWAVE,SM,OVEW)
	    SMAT(JLS)=SM
	    FUST(JLS)=OVEW*FJ2*0.5*FAC1
	    
  250    CONTINUE
  300 CONTINUE
      
      IF (KTRL3.NE.0) THEN
	 CALL ELCROS(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
     1               CRRUTH,CREL,CRATIO)
      ENDIF

      IF (KTRL4.NE.0) THEN
	 CALL ELETC(LMAX,WN,ETA,SMAT,SIGLS,PHAS,FUST,
     1              FCROS,TCROS)
      ENDIF

      RETURN
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE COUPHS(LMAX,ETA,
     O                  SIG)
C=======================================================================
C
C     PROGRAM NAME : COUPHS.FOR
C
C     PURPOSE
C     CALCULATION OF COULOMB PHASE SHIFT UP TO MAXIMUM ANGULAR MOMENTUM 
C     FOR A GIVEN ETA.
C
C     REFERENCE
C     1. JOHN M.BLATT, VICTOR F.WEISSKOPF, THEORETICAL NUCLEAR PHYSICS,
C     (JOHN WILEY, NEW YORK, 1952) 330. EQ.2.35.
C     2. MILTON ABRAMOWITZ AND IRENE A.STEGUN, HANDBOOK OF MATHEMATICAL 
C     FUNCTIONS WITH FORMULAS, GRAPHS, AND MATHEMATICAL TABLES,
C     (NATIONAL BUREAU OF STANDARDS, WASHINGTON, 1964) 256-257. 
C     EQ.6.1.24, EQ.6.1.40, EQ.6.1.41.
C     3. JOHN R.TAYLOR, SCATTERING THEORY, (JOHN WILEY, NEW YORK, 1972) 
C     261. EQ.14.4. 
C
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C
C     RESULTS
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1)
C
C     EXAMPLE
C     LMAX    ETA     SIG(1/6        2/7        3/8        4/9       5/10)    
C      1     1.000     -.30164
C      1     2.000      .12964
C      2     1.000     -.30164     .48375
C      2     2.000      .12964    1.23679
C      9     1.000     -.30164     .48375     .94740    1.26915    1.51413
C                      1.71153    1.87667    2.01857    2.14293
C     10     1.000     -.30164     .48375     .94740    1.26915    1.51413
C                      1.71153    1.87667    2.01857    2.14293    2.25358
C     10    10.000     1.23654    2.70766    4.08107    5.36041    6.55070
C                      7.65784    8.68822    9.64829   10.54435   11.38233
C
C-----------------------------------------------------------------------      
C      
      PARAMETER(KL=200)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   SIG(KL)
C
      IF (ETA.LT.10.D0) THEN
         ETA2=ETA*ETA
         ETA2A=2.0D0*ETA
         ETA6=ETA2+16.0D0
         SIGMA0=-(ETA/(12.D0*ETA6))*(1.D0+(ETA2-48.D0)/(30.D0*(ETA6**2))
     1          +((ETA2-160.D0)*ETA2+1280.D0)/(105.D0*(ETA6**4)))
     2          -ETA+(ETA/2.D0)*DLOG(ETA6)+3.5D0*DATAN(0.25D0*ETA)
     3          -(DATAN(ETA)+DATAN(0.5D0*ETA)+DATAN(ETA/3.D0))
      ELSE
      EINV1=1.00D0/ETA
      EINV2=EINV1*EINV1
      EINV3=EINV1*EINV2
      EINV5=EINV3*EINV2
      EINV7=EINV5*EINV2
      EINV9=EINV7*EINV2
      SIGMA0=+0.7853981634D0+ETA*DLOG(ETA)-ETA
     1       -(0.08333333333D0*EINV1+0.00277777777D0*EINV3
     2       +0.00079365079D0*EINV5+0.00059523810D0*EINV7
     3       +0.00084175084D0*EINV9)
      ENDIF
      
      MODTPI=SIGMA0/6.2831853072D0
      FMODTP=MODTPI
      SIGMA0=SIGMA0-6.2831853072D0*FMODTP

CCCCC COULOMB WAVE PHASE SHIFT

      LMAX1=LMAX-1
      SIG(1)=SIGMA0
      DO 100 L=1,LMAX1
         SS=L
         SIG(L+1)=SIG(L)+DATAN(ETA/SS)
  100 CONTINUE

      RETURN
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE POTEN(ICONT,NXMIN,NXMAX,XMES,V0,X0,A0,
     O                 VR)
C=======================================================================
C
C     PROGRAM NAME : POTEN.FOR 
C
C     PURPOSE
C     POTENTIAL ENERGY CALCULATION.
C     
C     REFERENCE
C     SAMUEL S.M.WONG, INTRODUCTORY NUCLEAR PHYSICS, (PRENTICE-HALL,
C     ENGLEWOOD CLIFFS,1990) 346-348.
C
C     INPUT DATA
C     ICONT  : INTEGER OF 1-DIGIT (TO SEE CONTROL CARD).     
C              M DENOTES THE TYPE OF THE POTENTIAL FORM.
C     NXMIN  : THE NUMBER OF MINIMUM IN INTEGRATION RANGE.
C     NXMAX  : THE NUMBER OF MAXIMUM IN INTEGRATION RANGE.
C     XMES   : MESH SIZE.
C     V0     : THE DEPTH PARAMETER OF POTENTIAL WELL.
C     X0     : THE RADIUS PARAMETER.
C     A0     : THE DIFFUSENESS PARAMETER.
C     
C     RESULT
C     VR(KX=500)  : POTENTIAL ENERGY.(nx+1)
C
C     CONTROL CARD.
C     ICONT  = 1 :  WOODS-SAXON POTENTIAL FORM.
C        VPONT = -V0/(1+EXP((X-X0)/A0))
C     ICONT  = 2 :  SPIN-ORBIT POTENTIAL FORM.
C        VPONT = -2.0*V0/A0*EXP((X-X0)/A0)/(1+EXP((X-X0)/A0))**2/X
C          REF. SAMUEL S.M. WONG, INTRODUCTORY NUCLEAR PHYSICS, 
C          (PRENTICE-HALL. INC. NEW YORK, 1990) 347.
C     ICONT  = 3 :  DERIVATIVE OF W.S. POTENTIAL FORM.
C                   (SURFACE INTERACTION.)
C        VPONT = -4.0*V0*EXP((X-X0)/A0)/(1+EXP((X-X0)/A0))**2
C     ICONT  = 4 :  COULOMB POTENTIAL FORM.
C        VPONT = V0*e**2/X0*(3-(X/X0)**2)       FOR X < X0  
C        VPONT = V0*e**2/X                      FOR X > X0 
C        V0    : THE PRODUCT OF CHARGE NUMBERS, Z1*Z2.
C        X0    : THE COULOMB RADIUS.
C     ICONT  = 5 :  GAUSSIAN POTENTIAL FORM.
C        VPONT = V0*EXP(-((X-X0)/A0)**2)
C     ICONT  = 6 :  YUKAWA POTENTIAL FORM.
C        VPONT = -V0*EXP(-(X-X0)/A0)/X
C     ICONT  = 7 :  HARMONIC OSCILLATOR POTENTIAL FORM (r**2) 
C        VPONT = V0*(X/A0)**2
C
C-----------------------------------------------------------------------      
C      
      PARAMETER(KX=500)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   VR(KX)
C
      MAX=NXMAX
      MIN=NXMIN
      MIN1=MIN+1
C
CCCCC PRINT ERROR MESSAGE     
      IF (MAX.GT.KX) THEN 
	 PRINT 100, MAX 
	 STOP
      ENDIF
  100 FORMAT('  CAN NOT CALCULATE THE POTENTIAL.', 
     1     /,'  THE CALCULATION REQUIRES A LARGER DIMENSION.', 
     2     /,'  INCREASE KX, SO THAT KX > ',I5)
CCCCC

      DO 200 NX=1,KX
  200 VR(NX)=0.0D0

      GOTO (1100,1200,1300,1400,1500,1600,1700) ICONT

 1100 DO 1110 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=-V0/(1.D0+EXPX)
 1110 CONTINUE
      RETURN
 1200 V01=-2.0D0*V0/A0
      DO 1210 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=V01*EXPX/(1.0D0+EXPX)**2/XX
 1210 CONTINUE
      RETURN
 1300 DO 1310 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP((XX-X0)/A0)
	 VR(NX)=-4.0D0*V0*EXPX/(1.0D0+EXPX)**2
 1310 CONTINUE
      RETURN
 1400 VCLFC2=1.439865D0*V0
      VCLFC1=0.5D0*VCLFC2/X0
      DO 1410 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 IF(XX.LT.X0) THEN
	    VR(NX)=VCLFC1*(3.0D0-(XX/X0)**2)
	 ELSE
	    VR(NX)=VCLFC2/XX
	 ENDIF
 1410 CONTINUE
      RETURN
 1500 DO 1510 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 XY=((XX-X0)/A0)**2
	 EXPX=DEXP(-XY)
	 VR(NX)=-V0*EXPX
 1510 CONTINUE
      RETURN
 1600 DO 1610 NX=MIN1,MAX
	 XX=DFLOAT(NX-1)*XMES
	 EXPX=DEXP(-(XX-X0)/A0)
	 VR(NX)=-V0*EXPX/XX
 1610 CONTINUE
      RETURN
 1700 DO 1710 NX=MIN,MAX
	 XX=DFLOAT(NX-1)*XMES
	 VR(NX)=V0*(XX/A0)**2
 1710 CONTINUE
      RETURN
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE DISWV(L1,J2,NXMIN,NXMAX,XMES,ECM,WN,ETA,EXSGRI,
     1                  VRID,VSPIN,VFUS,
     O                  DWAVE,SMAT,CRFUS)           
C======================================================================
C
C     PROGRAM NAME : DISWV1.FOR
C
C     PURPOSE
C     DISTORTED WAVE, S-MATRIX AND FUSION CROSS SECTION CALCULATIONS 
C     BY NUMEROV METHOD FOR A GIVEN POTENTIAL ENERGY WITH SPIN-ORBIT
C     INTERACTION.
C
C     REFERENCE
C     JOHN M.BLATT AND VICTOR F.WEISSKOPF, THEORETICAL NUCLEAR PHYSICS,
C     (WILEY, NEW YORK,1952) 331. EQ.2.41.
C      
C     REQUIRED SUBROUTINE
C     COUFT(L1,ETA,RHOMX,  FC,GC,FCP,GCP)
C     
C     INPUT DATA
C     L1     : ANGULAR MOMENTUM.(l)
C     J2     : TOTAL ANGULAR MOMENTUM.(2j)
C     NXMIN  : MINIMUM NUMBER OF INTEGRATION RANGE.
C     NXMAX  : MAXIMUM NUMBER OF INTEGRATION RANGE.
C     XMES   : MESH SIZE.
C     ECM    : CENTER OF MASS ENERGY.
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS REDUCED MASS AND k THE WAVE NUMBER.
C     EXSGRI : EXPONENTIAL(i * COULOMB PHASE SHIFT). 
C     VRID(KX=500)  :  POTENTIAL ENERGY.(nx+1)
C     VSPIN(KX=500) :  SPIN-ORBIT INTERACTION.(nx+1)
C     VFUS(KX=500)  :  FUSION POTENTIAL ENERGY.(nx+1)
C
C     RESULTS
C     DWAVE(KX=500) : DISTORTED WAVE.(nx+1)
C     SMAT   : PARTIAL S-MATRIX.
C     CRFUS  : PARTIAL FUSION CROSS SECTION.
C
C----------------------------------------------------------------------
C
      PARAMETER(KX=500)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   VRID(KX),VSPIN(KX),VFUS(KX),DWAVE(KX)
      COMPLEX*16     EXSGRI,VRID,DWAVE,SMAT
      COMPLEX*16     URI1,URI2,URI1H,URI2H,URI3H  
      COMPLEX*16     ZERO,TTI,CON1,B1,CAB,D,CL,COR
      COMPLEX*16     VRIT,ARI,BRI
C
      NXMX2=NXMAX+2
      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D0)
      RM=XMES*DFLOAT(NXMAX-1)
      TMU=WN**2/ECM
      HSQ=XMES**2
      HSQ12=HSQ/12.0D0
      SPFAC=DFLOAT((J2+2)*J2-4*L1*(L1+1)-3)/4.0D0
      YMG=1.0D-90
      YMP1=-2.0D-90
C
      IF (NXMX2.GT.KX) THEN
         PRINT 50, NXMAX,KX
         STOP
      ENDIF
   50 FORMAT(1H ,'NXMAX,KX=',2I5,/
     1       1H ,'INCREASE KX IN PARAMETER')
C
      DO 100 NX=1,NXMX2
         DWAVE(NX)=ZERO
  100 CONTINUE
C
      RHOMX=RM*WN
C
      CALL COUFT(L1,ETA,RHOMX,  FC,GC,FCP,GCP) 
C
      FC2D=FC
      GC2D=GC
      FCPM=FCP*WN
      GCPM=GCP*WN
C
      URI1H=ZERO
      URI2H=YMG
      IF (L1.EQ.1) URI2H=YMP1
C
      NXMN1=NXMIN+1
      DO 200 NX=NXMN1,NXMX2
         CFFCLL=L1*(L1+1)
         XX=DFLOAT(NX-1)*XMES
         XSQIN=1.0D0/(XX*XX)
         VRIT=-(ECM-VRID(NX))*TMU 
         ARI=VRIT+VSPIN(NX)*TMU*SPFAC+CFFCLL*XSQIN
         BRI=1.0D0-HSQ12*ARI
         DWAVE(NX)=URI2H/BRI
         URI3H=(2.0D0+(HSQ*ARI/BRI))*URI2H-URI1H
         URI1H=URI2H
         URI2H=URI3H
  200 CONTINUE
C
      N5=NXMAX-3
      DENOM=1.0D0/(12.0D0*XMES)
      URI1=DWAVE(N5+3)
      URI2=(8.0D0*(DWAVE(N5+4)-DWAVE(N5+2)) 
     1          -DWAVE(N5+5)+DWAVE(N5+1))*DENOM
      CON1=-URI2/URI1
      B1=CON1*FC2D+FCPM
      CAB=-CON1*(GC2D+TTI*FC2D)
      D=-GCPM-TTI*FCPM
      CL=B1/(CAB+D)
      CON1=FC2D+CL*(GC2D+TTI*FC2D)
      COR=EXSGRI*CON1/URI1
      SMAT=1.D0+2.D0*TTI*CL
C
      DO 300 NX=NXMN1,NXMX2
         DWAVE(NX)=DWAVE(NX)*COR
  300 CONTINUE
C
      CRFUS=0.0D0
       DO 400 NX=NXMIN,NXMAX
         CRFUS=CRFUS-DWAVE(NX)*CONJG(DWAVE(NX))*VFUS(NX)*XMES
  400 CONTINUE
C
      RETURN
      END 
CCCCCC
CCCCCC
C========================================================================
      SUBROUTINE COUFT(L1,ETA,RHO,
     O                 F,G,FP,GP)
C========================================================================
C
C     PROGRAM NAME : COUFT.FOR
C
C     PURPOSE
C     CALCULATIONS OF PARTIAL COULOMB WAVE FUNCTIONS FOR A GIVEN ETA, 
C     RHO AND ANGULAR MOMENTUM.
C
C     REFERENCE
C     A.R.BARNET, D.H.FENG, J.W.STEED AND L.J.B.GOLDFARB,
C     COMPUTER PHYS. COMMUNICATIONS 8 (1974) 377.
C
C     INPUT DATA 
C     L1     : ANGULAR MOMENTUM.(l)
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v)
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     RHO    = k*r = sqrt(mu*Ecm)*r/h_BAR
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C
C     RESULTS
C     F      : REGULAR COULOMB WAVE FUNCTION AT RHO.(l)
C     FP     : DERIVATIVE OF F AT RHO.(l)
C     G      : IRREGULAR COULOMB WAVE FUNCTION AT RHO.(l)
C     GP     : DERIVATIVE OF G AT RHO.(l)
C
C     EXAMPLE
C     L1    ETA    RHOMX       F           G            FP           GP
C     1    1.000   1.000   7.5182E-02  3.78273E+00  1.7142E-01  -4.67592E+00    
C     1    2.000   2.000   8.1166E-02  5.06994E+00  1.2006E-01  -4.82089E+00   
C     2    1.000   1.000   1.4785E-02  1.26415E+01  4.7087E-02  -2.73742E+01   
C     2    2.000   2.000   2.9892E-02  1.05780E+01  5.5013E-02  -1.39859E+01   
C    10   10.000   9.000   1.6896E-06  1.84188E+05  2.8045E-06  -2.86121E+05    
C    10   10.000  10.000   8.1838E-06  4.21596E+04  1.2269E-05  -5.89840E+04    
C
C--------------------------------------------------------------------------      
C      
	IMPLICIT REAL*8(A-H,O-Z)
      REAL        K,K1,K2,K3,K4,M1,M2,M3,M4
C
      MINL=L1
      MAXL=L1
      ACCUR=1.0D-10
      STEP=999.0D0
      PACE=STEP
      ACC=ACCUR
      IF(PACE.LT.100.0D0)PACE=100.0D0
      IF(ACC.LT.1.0D-15.OR.ACC.GT.1.0D-6) ACC = 1.0D-6
      R=RHO
      KTR=1
      LMAX=MAXL
      LMIN1=MINL+1
      XLL1=DFLOAT(MINL*LMIN1)
      ETA2=ETA*ETA
      TURN=ETA+DSQRT(ETA2+XLL1)
      IF(R.LT.TURN.AND.DABS(ETA).GE.1.0D-6)KTR=-1
      KTRP=KTR
      GOTO 2
1     R=TURN
      TF=F
      TFP=FP
      LMAX=MINL
      KTRP=1
2     ETAR=ETA*R
      RHO2=R*R
      PL=DFLOAT(LMAX+1)
      PMX=PL+0.5
      FP=ETA/PL+PL/R
      DK=ETAR*2.00D0
      DEL=0.0D0
      D=0.0D0
      F=1.0D0
      K=(PL*PL-PL+ETAR)*(2.0D0*PL-1.0D0)
      IF(PL*PL+PL+ETAR.NE.0.0D0)GOTO3
      R=R+1.0D-6
      GOTO 2
3     H=(PL*PL+ETA2)*(1.0D0-PL*PL)*RHO2
      K=K+DK+PL*PL*6.0D0
      D=1.0D0/(D*H+K)
      DEL=DEL*(D*K-1.0D0)
      IF(PL.LT.PMX)DEL=-R*(PL*PL+ETA2)*(PL+1.0D0)*D/PL
      PL=PL+1.0D0
      FP=FP+DEL
      IF(D.LT.0.0D0)F=-F
      IF(PL.GT.20000.D0) GOTO 11
      IF(DABS(DEL/FP).GE.ACC) GOTO 3
      FP=F*FP
      IF(LMAX.EQ.MINL) GOTO 5
CC
5     IF(KTRP.EQ.-1) GOTO 1
      P=0.0D0
      Q=R-ETA
      PL=0.0D0
      AR=-(ETA2+XLL1)
      AI=ETA
      BR=2.0D0*Q
      BI=2.0D0
      WI=2.0D0*ETA
      DR=BR/(BR*BR+BI*BI)
      DI=-BI/(BR*BR+BI*BI)
      DP=-(AR*DI+AI*DR)
      DQ=(AR*DR-AI*DI)
6     P=P+DP
      Q=Q+DQ
      PL=PL+2.0D0
      AR=AR+PL
      AI=AI+WI
      BI=BI+2.0D0
      D=AR*DR-AI*DI+BR
      DI=AI*DR+AR*DI+BI
      T=1.0D0/(D*D+DI*DI)
      DR=T*D
      DI=-T*DI
      H=BR*DR-BI*DI-1.0D0
      K=BI*DR+BR*DI
      T=DP*H-DQ*K
      DQ=DP*K+DQ*H
      DP=T
      IF(PL.GT.46000.D0) GOTO 11
      IF(DABS(DP)+DABS(DQ).GE.(DABS(P)+DABS(Q))*ACC)GOTO6
      P=P/R
      Q=Q/R
      G=(FP-P*F)/Q
      GP=P*G-Q*F
      W=1.0D0/DSQRT(FP*G-F*GP)
      G=W*G
      GP=W*GP
      IF(KTR.EQ.1) GOTO 8
      F=TF
      FP=TFP
      LMAX=MAXL
      IF(RHO.LT.0.2*TURN)PACE=999.0D0
      R3=1.0D0/3.0D0
      H=(RHO-TURN)/(PACE+1.0D0)
      H2=0.5D0*H
      I2=INT(PACE+0.001D0)
      ETAH=ETA*H
      H2LL=H2*XLL1
      S=(ETAH+H2LL/R)/R-H2
7     RH2=R+H2
      T=(ETAH+H2LL/RH2)/RH2-H2
      K1=H2*GP
      M1=S*G
      K2=H2*(GP+M1)
      M2=T*(G+K1)
      K3=H*(GP+M2)
      M3=T*(G+K2)
      M3=M3+M3
      K4=H2*(GP+M3)
      RH=R+H
      S=(ETAH+H2LL/RH)/RH-H2
      M4=S*(G+K3)
      G=G+(K1+K2+K3+K2+K4)*R3
      GP=GP+(M1+M2+M2+M3+M4)*R3
      IF(( G.GT.1.0D65 ).OR.( GP.GT.1.0D65 ))GOTO11
C      IF(( G.GT.1.0D36 ).OR.( GP.GT.1.0D36 ))GOTO11
      R=RH
      I2=I2-1
      IF(I2.GE.0) GOTO 7
      W=1.0D0/(FP*G-F*GP)
    8 F=W*F
      FP=W*FP
C      
  500 RETURN
C
   11 W=0.0D0
      G=0.0D0
      GP=0.0D0
      GOTO 8
      END
CCCCCC
CCCCCC
C=======================================================================
      SUBROUTINE ELCROS(LMAX,NAMAX,WN,ETA,SIG,SMAT,THETAD,
     O                   CRRUTH,CREL,CRATIO)
C=======================================================================
C
C     PROGRAM NAME : ELCROS1.FOR
C
C     PURPOSE
C     RUTHERFORD, ELASTIC SCATTERING CROSS SECTION AND RATIO
C     CALCULATIONS WITH SPIN-ORBIT INTERACTION.
C
C     REQUIRED SUBROUTINE
C     LEGNL0(TH,LMAX,  PK)
C     
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     NAMAX  : MAXIMUM NUMBER OF ANGLE.
C     ECM    : CENTER OF MASS ENERGY.
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C     SIG(KL=200)   : COULOMB PHASE SHIFT.(l+1)
C     SMAT(KL2=KL*2): S-MATRIX, CONSIDER SPIN DOWN AND UP.(l+j+s)
C     THETAD(KA=180): THE ANGLE IN THE UNIT OF DEGREE.(na)
C
C     RESULTS
C     CRRUTH(KA=180): RUTHERFORD SCATTERING CROSS SECTION.(na)
C     CREL(KA=180)  : ELASTIC SCATTERING CROSS SECTION.(na)
C     CRATIO(KA=180): RATIO OF ELASTIC TO RUTHERFORD CROSS SECTION.(na) 
C
C------------------------------------------------------------------------
C      
      PARAMETER(IW=6,KL=200,KA=180,KL2=KL*2)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   THETAD(KA),PK(KA),ANAL(KA),
     1            CRRUTH(KA),CREL(KA),CRATIO(KA)
      DIMENSION   SIG(KL),SMAT(KL2),G(KL),H(KL)
      COMPLEX*16     ZERO,TTI,FCC,CPH,EXPSIG,SMAT,G,H,GSUM,HSUM
C
      PI=4.0D0*DATAN(1.0D00)
      ZERO=(0.0D0,0.0D0)
      TTI =(0.0D0,1.0D0)
      LMAX1=LMAX+1
C
      DO 300 NA=1,NAMAX
         TH=THETAD(NA)*PI/180.0D0
         CSTH=DCOS(TH)
         IPKL1=0
         ABSTH=1.0D0-DABS(CSTH)
         IF (ABSTH.LE.1.0D-10) IPKL1=1
C
         CALL LEGNL(TH,LMAX1,  PK)
C                                    
         HSUM=ZERO
         GSUM=ZERO
         DO 200 L1=1,LMAX
            L=L1-1
            L2=L1+1
            L21=2*L+1
            FL2=DFLOAT(L21)
            FL1=DFLOAT(L1)
            FL0=DFLOAT(L)
            EXPSIG=CDEXP(2.D0*TTI*SIG(L1))
            JUP=L*2+1
            JDN=L*2
            IF (L1.EQ.1) THEN
               G(L1)=(FL2-FL1*SMAT(JUP))*EXPSIG*PK(L1)
               H(L1)=ZERO
            ELSE
            G(L1)=(FL2-FL1*SMAT(JUP)-FL0*SMAT(JDN))*EXPSIG*PK(L1)
            IF (IPKL1.EQ.1) THEN
               PKL1=0.0D0
            ELSE
            PKL1=FL0*FL1/FL2*(PK(L)-PK(L2))/DSQRT(1.0D0-CSTH**2)
            ENDIF
            H(L1)=(SMAT(JDN)-SMAT(JUP))*EXPSIG*PKL1
            ENDIF
            GSUM=GSUM+G(L1)*TTI/(2.D0*WN)
            HSUM=HSUM+H(L1)*TTI/(2.D0*WN)
  200   CONTINUE

        FCC=ZERO
        IF (ETA.NE.0.0) THEN
           SIN2=(DSIN(0.5D0*TH))**2
           FC0=ETA/(2.D0*WN*SIN2)
           CPH=DCMPLX(0.D0,-ETA*DLOG(SIN2)+2.D0*SIG(1))
           FCC=FC0*CDEXP(CPH)
        ENDIF
        
        GSUM=GSUM-FCC
        CREL(NA)  =10.D0*(GSUM*CONJG(GSUM)+HSUM*CONJG(HSUM))
        CRRUTH(NA)=10.D0*FC0**2
        ANAL(NA)  =20.D0*AIMAG(GSUM*CONJG(HSUM))/CREL(NA) 
        IF (ETA.EQ.0.0) CRRUTH(NA)=1.0D0
        CRATIO(NA)=CREL(NA)/CRRUTH(NA)
  300 CONTINUE

      WRITE(IW,900)
      DO 500 NA=1,NAMAX
         WRITE(IW,910) THETAD(NA),CREL(NA),CRRUTH(NA),CRATIO(NA),
     1                 ANAL(NA),THETAD(NA)
  500 CONTINUE
  900 FORMAT(//,1X,'  ANGLE  ELASTIC CS    RUTH. CS       RATIO '
     1             ' ANAL POWER  ANGLE'/
     2          1X,'  (DEG)       (MB)        (MB)              '
     3             '             (DEG)'/)
  910 FORMAT(1X,F7.2,4E12.3,F7.2)

      RETURN
      END
CCCCCC   
CCCCCC
C=====================================================================C              
      SUBROUTINE LEGNL(TH,LMAX,
     O                  PK)
C=====================================================================C              
C
C     PROGRAM NAME : LEGNL0.FOR
C
C     PURPOSE
C     LEGENDRE FUNCTION CALCULATION AT GIVEN TH AND LMAX. 
C
C     REFERENCE
C     MILTON ABRAMOWITZ AND IRENE A.STEGUN, HANDBOOK OF MATHEMATICAL
C     FUNCTIONS WITH FORMULAS, GRAPHS, AND MATHEMATICAL TABLES,
C     (NATIONAL BUREAU OF STANDARDS, WASHINGTON 1964) 334. EQ.8.5.3.
C     
C     INPUT DATA
C     TH     : THE ANGLE IN THE UNIT OF RADIAN.
C     LMAX     : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C
C     RESULTS
C     PK(KL=200)   : TO CACULATE IN Z-PROJECTION TO "0".(l+1)
C
C      EXAMPLE
C       TH    LMAX   PK(1/6        2/7        3/8        4/9       5/10)
C      1.000    1    1.00000
C      1.000    2    1.00000     .54030
C      2.000    2    1.00000    -.41614
C      1.000   10    1.00000     .54030    -.06211    -.41613    -.34688
C                   -0.00445     .28465     .28944     .04415    -.21222
C      2.000   10    1.00000    -.41614    -.24023     .44405    -.14320
C                   -0.24796     .30852    -.02589    -.24975     .21933
C
C-----------------------------------------------------------------------
C
       PARAMETER( KL=200 )      
       IMPLICIT REAL*8(A-H,O-Z)
       DIMENSION   PK(KL)
C
      IF(LMAX.GE.KL) THEN
         PRINT 500,LMAX
      END IF
  500 FORMAT('  CAN NOT CALCULATE THE POTENTIAL.',
     1     /,'  THE CALCULATION REQUIRES A LAGER DIMENSION.',
     2     /,'  INCREASE KL, SO THAT KL >',I5)
C
      PK(1)=1.0D0
      PK(2)=DCOS(TH)
C
      DO 100 L=3,LMAX
         S=DFLOAT(L)
         PK(L)=(1.D0/(S-1.D0))*((2.D0*S-3.D0)
     1            *DCOS(TH)*PK(L-1)-(S-2.D0)*PK(L-2))
  100 CONTINUE                                                                 
      RETURN
      END
CCCCCC
CCCCCC
C======================================================================
      SUBROUTINE ELETC(LMAX,WN,ETA,SMAT,SIGLS,PHAS,FUST,
     O                  FCROS,RECROSS)
C======================================================================
C
C     PROGRAM NAME IS ELETC1.FOR
C
C     PURPOSE
C     TOTAL FUSION AND REACTION CROSS SECTION CALCULATIONS FOR GIVEN
C     S-MATRICES AND PARTIAL FUSION CROSS SECTION WITH SPIN-ORBIT
C     INTERACTION.
C     
C     INPUT DATA
C     LMAX   : THE NUMBER OF MAXIMUM ANGULAR MOMENTUM.(l+1)
C     WN     : WAVE NUMBER.
C     ETA    : SOMMERFELD PARAMETER.
C     ETA    = Z1*Z2*e**2/(h_BAR*v) 
C            = mu*Z1*Z2*e**2/(h_BAR**2*k)
C     WHERE mu IS THE REDUCED MASS AND k THE WAVE NUMBER.
C     SMAT(KL2=2*200)  : S-MATRIX, CONSIDER SPIN DOWN AND UP.(l+j+s)
C     FUST(KL2=2*200)  : FUSION CROSS SECTION, SPIN DOWN AND UP.(l+j+s)
C
C     RESULTS
C     FCROS   : TOTAL FUSION CROSS SECTION.
C     RECROSS : TOTAL REACTION CROSS SECTION.
C
C------------------------------------------------------------------------
C      
      PARAMETER(IW=6,KL=200,KL2=KL*2)
	IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION   FUST(KL2),ABSS(KL2),ABSC(KL2),PHAS(KL2),PHAC(KL2)
	DIMENSION   SIGLS(KL2),SIGLC(KL2)
      DIMENSION   TCOE(KL2),RECROS(KL2),TCROS(KL2)
      DIMENSION   SMAT(KL2),CL(KL2)
      COMPLEX*16     TTI,SMAT,CL,SMM1,SMM2
C
      TTI=(0.0D0,1.0D0)
      PI=4.0D0*DATAN(1.0D00)
      JMAX=LMAX*2-1
C
      DO 10 L=1,JMAX
         ABSS(L)=0.0D0      
         ABSC(L)=0.0D0    
         PHAS(L)=0.0D0      
         PHAC(L)=0.0D0
   10 CONTINUE
C      
c      FCROS=FUST(1)
c      FCRO1=0.0D0
c      FCRO2=0.0D0
c      DO 100 L1=2,LMAX
c         L=L1-1
c         JUP=2*L+1
c         JDN=2*L
c         FL0=DFLOAT(L)
c         FCROS=FCROS+(FUST(JUP)+FUST(JDN))
c         FCRO1=FCRO1+(FUST(JUP)+FUST(JDN))*FL0
c         FCRO2=FCRO2+(FUST(JUP)+FUST(JDN))*FL0**2
c  100 CONTINUE
c         AVEL1=FCRO1/FCROS
c         AVEL2=FCRO2/FCROS
c         AVEL2=DSQRT(AVEL2)
C
c      TOCROSS=0.0D0
c      RECROSS=0.0D0
      ELCROSS=0.0D0
      AREA=PI/(WN*WN)
      DO 200 L1=1,LMAX
         L=L1-1
         JUP=2*L+1
         JDN=2*L
         FJUP=DFLOAT(JUP+1)
         FJDN=DFLOAT(JDN)
         IF (L1.EQ.1) THEN
            CL(JUP)    =(SMAT(JUP)-1.)/(2.*TTI)
c            TCOE(JUP)  =1.D0-SMAT(JUP)*CONJG(SMAT(JUP))
c            RECROS(JUP)=10.D0*TCOE(JUP)*FJUP*AREA*0.5D0
c            RECROSS=RECROSS+(RECROS(JUP))
         ELSE
         CL(JUP)    =(SMAT(JUP)-1.D0)/(2.D0*TTI)
c         TCOE(JUP)  =1.D0-SMAT(JUP)*CONJG(SMAT(JUP))
c         RECROS(JUP)=10.D0*TCOE(JUP)*FJUP*AREA*0.5D0
         CL(JDN)    =(SMAT(JDN)-1.D0)/(2.D0*TTI)
c         TCOE(JDN)  =1.D0-SMAT(JDN)*CONJG(SMAT(JDN))
c         RECROS(JDN)=10.D0*TCOE(JDN)*FJDN*AREA*0.5D0
c         RECROSS=RECROSS+(RECROS(JUP)+RECROS(JDN))
         ENDIF
         IF (ETA.EQ.0.0D0) THEN
            IF (L1.EQ.1) THEN
                SMM1=1.0D0-SMAT(JUP)
                SMM12=SMM1*CONJG(SMM1)*FJUP*0.5D0
c                TCROS(JUP)=10.0D0*(1.0D0-REAL(SMAT(JUP)))*FJUP*AREA
c                TOCROSS=TOCROSS+TCROS(JUP)
                ELCROSS=ELCROSS+10.0*SMM12*AREA
            ELSE     
            SMM1=1.0D0-SMAT(JUP)
            SMM2=1.0D0-SMAT(JDN)
            SMM12=SMM1*CONJG(SMM1)*FJUP*0.5D0
            SMM22=SMM2*CONJG(SMM2)*FJDN*0.5D0
c            TCROS(JUP)=10.0D0*(1.0-REAL(SMAT(JUP)))*FJUP*AREA
c            TCROS(JDN)=10.0D0*(1.0-REAL(SMAT(JDN)))*FJDN*AREA
c            TOCROSS=TOCROSS+TCROS(JUP)+TCROS(JDN)
            ELCROSS=ELCROSS+10.0D0*(SMM12+SMM22)*AREA
            ENDIF
         ENDIF 
  200 CONTINUE

      DO 300 L=1,JMAX
	   L1=L/2
         X1=REAL(SMAT(L))
         X2=AIMAG(SMAT(L))
         ABSM=(X1**2)+(X2**2)
         IF (ABSM.EQ.0.0D0) GO TO 300
         ABSS(L)=DSQRT(ABSM)
         PHAS(L)=DATAN2(X2,X1)/2.D0
	   SIGLS(L)=40.0D0*AREA*DFLOAT(2*L1+1)*DSIN(PHAS(L))**2
         X1=REAL(CL(L))
         X2=AIMAG(CL(L))
         ABSM=(X1**2)+(X2**2)
         IF (ABSM.EQ.0.0D0) GO TO 300
         ABSC(L)=DSQRT(ABSM)
         PHAC(L)=DATAN2(X2,X1)
	   SIGLC(L)=40.0D0*AREA*DFLOAT(2*L1+1)*ABSM
  300 CONTINUE

      LH=0     
c      DO 400 L=1,JMAX
c         IF (TCOE(L).GE.0.5D0) LH = L 
c  400 CONTINUE
      LSA=LH/2
      HL=DFLOAT(LH)-0.5D0
      IF (ETA.NE.0.0D0) THEN
         A=1.0D0+(HL/ETA)**2
         SAR=(ETA/WN)*(1.0D0+DSQRT(A))
      ELSE
      SAR=HL/WN
      ENDIF

      IF (ETA.EQ.0.0D0) THEN
         WRITE(IW,900)
      ELSE
      WRITE(IW,902) 
      ENDIF

      DO 500 L=1,JMAX
         L1=L/2
         J2=L
         LMODE=MOD(L,2)
         IF (LMODE.EQ.0) J2=L-1
         IF (ETA.EQ.0.0D0) THEN
            WRITE(IW,910) L1,J2,ABSS(L),PHAS(L),SIGLS(L),SIGLC(L)
         ELSE
         WRITE(IW,912) L1,J2,ABSS(L),PHAS(L),SIGLS(L),SIGLC(L)
c            WRITE(IW,910) L1,J2,ABSS(L),PHAS(L),TCOE(L),
c     1                    FUST(L),RECROS(L),TCROS(L),L1,J2
c         ELSE
c         WRITE(IW,912) L1,J2,ABSS(L),PHAS(L),TCOE(L),
c     1                 FUST(L),RECROS(L),L1,J2
         ENDIF
  500 CONTINUE
      IF (ETA.EQ.0.0D0) THEN
c         WRITE(IW,920) FCROS,RECROSS,TOCROSS
         WRITE(IW,930) ELCROSS
      ELSE
c      WRITE(IW,920) FCROS,RECROSS
      ENDIF
c      WRITE(IW,940) AVEL1
c      WRITE(IW,950) AVEL2
c      WRITE(IW,960) LSA
c      WRITE(IW,970) SAR

  900 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
     1          1H ,'  L  J2',' ABS S-MAT',' PHA S-MAT',/)
  902 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
     1          1H ,'  L  J2',' ABS S-MAT',' PHA S-MAT',/)
c  900 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
c     1          1H ,'  L  J2',' ABS S-MAT',' PHA S-MAT',
c     2              '   T-COEFF',4X,'FUSION','  REACTION','     TOTAL'
c     3          ,'   L  J2'/21X,'(RAD)',17X,'(MB)',6X,'(MB)'/)
c  902 FORMAT(//,1H ,'PARTIAL WAVE INFORMATION'//
c     1          1H ,'  L  J2',' ABS S-MAT',' PHA S-MAT',
c     2              '   T-COEFF',4X,'FUSION','  REACTION'
c     3          ,'   L  J2'/21X,'(RAD)',17X,'(MB)',6X,'(MB)'/)
  910 FORMAT(2I4,6E10.3,2I4)
  912 FORMAT(2I4,5E10.3,2I4)
  920 FORMAT(1H ,'TOTAL'32X,3E10.3)
  930 FORMAT(1H ,'TOTAL ELASTIC CROSS SECTION',20X,E10.3)
  940 FORMAT(1H ,'AVERAGE L-VALUE         =',E10.3)
  950 FORMAT(1H ,'AVERAGE L-SQUARE ROOT   =',E10.3)
  960 FORMAT(1H ,'STRONG ABSORPTION L     =',I5)
  970 FORMAT(1H ,'STRONG ABSORPTION RADIUS=',E10.3)
C 
      RETURN
      END
